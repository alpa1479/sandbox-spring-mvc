plugins {
    id "idea"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "edu.sandbox"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:${Versions.SPRING_BOOT_DEPENDENCIES}")
            }

            dependency("org.projectlombok:lombok:${Versions.LOMBOK}")
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

task printManagedVersions {
    group "custom help"
    description "prints available dependencies"

    doLast {
        dependencyManagement.managedVersions.each {
            println it
        }
    }
}